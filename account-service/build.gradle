plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.1.5.RELEASE'
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id "jacoco"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-oauth2'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-bus-amqp'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix'
    implementation 'org.springframework.cloud:spring-cloud-netflix-hystrix-stream'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.2'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'javax.xml.bind:jaxb-api'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'
    testImplementation 'com.jayway.jsonpath:json-path:2.4.0'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

task classpathJar(type: Jar) {
    inputs.files sourceSets.test.runtimeClasspath

    archiveName = "runboot-classpath.jar"
    doFirst {
        manifest {
            def classpath = sourceSets.test.runtimeClasspath.files
            attributes "Class-Path": classpath.collect {f -> f.toURI().toString()}.join(" ")
        }
    }
}

bootRun {
    classpath = classpathJar.outputs.files
    main = bootJar.mainClassName
    systemProperty "spring.profiles.active", "${profile}"
}

test {
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}
